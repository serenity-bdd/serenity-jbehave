import org.ajoberstar.gradle.git.release.opinion.Strategies
import org.ajoberstar.grgit.Grgit

buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.5'
        classpath 'org.ajoberstar:gradle-git:1.6.0-rc.1'
    }
}

apply plugin: 'org.ajoberstar.release-base'
release {
    grgit = Grgit.open()
    versionStrategy Strategies.FINAL
    versionStrategy Strategies.PRE_RELEASE
    defaultVersionStrategy Strategies.SNAPSHOT
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = 1.8
targetCompatibility = 1.8
group = 'net.serenity-bdd'

repositories {
    mavenLocal()
    jcenter()
    maven {
        url 'http://dl.bintray.com/serenity/maven/'
    }
}

ext {
    bintrayBaseUrl = 'https://api.bintray.com/maven'
    bintrayRepository = 'maven'
    bintrayPackage = 'serenity-jbehave'
    projectDescription = 'Serenity JBehave integration'

    if (!project.hasProperty("bintrayUsername")) {
        bintrayUsername = 'wakaleo'
    }
    if (!project.hasProperty("bintrayApiKey")) {
        bintrayApiKey = ''
    }

    if (!project.hasProperty("bintrayApiKey")) {
        bintrayApiKey = ''
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "${gradleVersion}"
}

test {
    include '**/When*'
    exclude '**/*$*'
    exclude '**/samples/**'
    exclude '**/*Sample*'
    maxParallelForks = Runtime.runtime.availableProcessors()
    testLogging {
        exceptionFormat = 'full'
//        showStandardStreams = true
    }
}

jar {
    println "Publishing $project.name:$project.group:$project.version"

    manifest {
        attributes("Implementation-Title": "Serenity JBehave Plugin",
                "Implementation-Version": project.version.toString())
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

configurations.all {
    resolutionStrategy {
        //failOnVersionConflict()
        force "junit:junit:${junitVersion}",
                "org.mockito:mockito-core:${mockitoCoreVersion}",
                "commons-collections:commons-collections:3.2.2",
                "com.thoughtworks.xstream:xstream:1.4.9",
                "xml-apis:xml-apis:1.4.01",
                "commons-codec:commons-codec:1.10",
                "org.objenesis:objenesis:2.6",
                "cglib:cglib-nodep:3.2.4",
                "org.seleniumhq.selenium:selenium-api:${seleniumVersion}",
                "org.springframework:spring-core:${springVersion}",
                "org.springframework:spring-beans:${springVersion}",
                "org.springframework:spring-context:${springVersion}"
    }
}

dependencies {
    compile("net.serenity-bdd:serenity-core:${project.serenityCoreVersion}") {
        exclude group:"org.seleniumhq.selenium", module:"htmlunit-driver"
    }

    compile "commons-logging:commons-logging:${commonsLoggingVersion}"

    compile "com.thoughtworks.paranamer:paranamer:${paranamerVersion}"

    compile("org.jbehave:jbehave-core:${jbehaveCoreVersion}") {
        exclude group: 'org.freemarker', module: 'freemarker'
        exclude group: 'junit', module: 'junit'
        exclude group: 'com.thoughtworks.xstream', module: 'xstream'
        exclude group: 'commons-collections', module: 'commons-collections'
        exclude group: 'com.thoughtworks.paranamer', module: 'paranamer'
    }

    compile ("com.github.valfirst:jbehave-junit-runner:${jbehaveJUnitRunner}") {
        exclude group: 'org.mockito', module: 'mockito-core'
        exclude group: 'junit', module: 'junit'
    }
    compile("org.reflections:reflections:${reflectionsVersion}") {
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'xml-apis', module: 'xml-apis'
    }
    compile("org.codehaus.groovy:groovy-all:${groovyVersion}")
    compile "junit:junit:${junitVersion}"

    testCompile("net.serenity-bdd:serenity-screenplay:${project.serenityCoreVersion}")
    testCompile("ch.qos.logback:logback-classic:${logbackVersion}") {
        exclude module: 'slf4j-api'
    }
    testCompile "org.assertj:assertj-core:${assertjVersion}"
    testCompile("net.serenity-bdd:serenity-spring:${project.serenityCoreVersion}") {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
    testCompile("net.serenity-bdd:serenity-test-utils:${project.serenityCoreVersion}") {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.jbehave', module: 'jbehave-core'
    }
    testCompile("net.serenity-bdd:serenity-sample-alternative-resources:${project.serenityCoreVersion}") {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.jbehave', module: 'jbehave-core'
    }
    testCompile("org.mockito:mockito-core:${mockitoCoreVersion}") {
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
        exclude group: 'org.objenesis', module: 'objenesis'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {

            println "Publishing $project.name:$project.group:$project.version"

            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name "$project.name"
                    description "$projectDescription"

                    url 'https://github.com/serenity-bdd/$project.name'
                    scm {
                        url 'scm:git:git@github.com:serenity-bdd/$project.name.git'
                        connection 'scm:git:git@github.com:serenity-bdd/$project.name.git'
                        developerConnection 'scm:git:git@github.com:serenity-bdd/$project.name.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'johnsmart'
                            name 'John Ferguson Smart'
                            email 'john.smart@wakaleo.com'
                        }
                    }
                }
                // Preserve compile-scope dependencies
                asNode().dependencies.'*'.findAll() {
                    it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
                        dep.name == it.artifactId.text()
                    }
                }.each() {
                    it.scope*.value = 'compile'
                }
            }
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            pom.artifactId = 'serenity-jbehave'
            pom.project {
                name = project.name
                description = projectDescription
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                url 'https://github.com/serenity-bdd/$project.name'
                scm {
                    url 'scm:git:git@github.com:serenity-bdd/$project.name.git'
                    connection 'scm:git:git@github.com:serenity-bdd/$project.name.git'
                    developerConnection 'scm:git:git@github.com:serenity-bdd/$project.name.git'
                }
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/license/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id 'johnsmart'
                        name 'John Ferguson Smart'
                        email 'john.smart@wakaleo.com'
                    }
                }
            }
            repository(url: bintrayBaseUrl + '/serenity/maven/serenity-jbehave') {
                authentication(userName: bintrayUsername, password: bintrayApiKey)
            }
        }
    }
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

task copyDeps(type: Copy) {
    from configurations.runtime + configurations.testCompile
    into project.projectDir.path + "/lib"
}
